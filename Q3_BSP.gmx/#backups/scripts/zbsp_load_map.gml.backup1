// 2019-08-22 06:11:10
///zbsp_load_map(filename, buildlevelmesh, builddebugmesh)
/*
    Loads Quake 3 map from zip/pk3 file, And returns ds_map containing the map information.
    =======================================================================================
    filename : direction to the Quake 3 map file. zip, pk3 and bsp files are supported.
    Feeding zip & pk3 files will make the script try to load the extra texture data & map metadata.
    
    buildlevelmesh : if set to true, the script will try to generate every face's mesh data into a vertex buffer.
    Then it can be used to render the levels with the vertex buffer.
    The list of buffers can be indexed from returned ds_map with the key "vb-faces" and each faces from the list can be rendered with vertex_submit() with [pr_triangle] primitive type.
    Each face has it's texture index data (see also : zbsp_load_lump_faces() for other data's indexes)
    And you can use that to fetch it's texture (ready to passed into vertex_submit) from the ds_list indexed with the key "textures-list".
    
    builddebugmesh : if set to true, the script will generate the Bounds visualization vertex buffer and Lightvol visualization vertex buffer.
    The bounds visualization vertex buffer contains the vertex buffer containing the set of line segments that visualizes the bounds data of Nodes, Leafs and Models.
    The buffer can be indexed from returned ds_map with the key "vb-debug-bbox" and can be rendered with vertex_submit() with [pr_linelist] primitive type.
    
    The lightvol visualization vertex buffer contains the vertex buffer containing the set of triangles that visualizes the LightVol (Uniform grid of light informations) with grid of cube.
    It visualizes the lights information about ambient light, directional light and its direction with a grid of 3D cubes,
    The buffer can be indexed from returned ds_map with the key "vb-debug-lightlvol" and can be rendered with vertex_submit() with [pr_trianglelist] primitive type.
*/

// ==================================================================
/// define variables
// ==================================================================
buildlevelmesh = argument1;
builddebugmesh = argument2;

// helper variables that will help you (& me) to index the bsp map data structure
zbsp_helper_vars();

// data related variables
var bspdata = ds_map_create(); // map containing the bsp data
var _filetype = string_delete(filename_ext(argument0), 1, 1);
var _filename = string_copy(filename_name(argument0), 1, string_pos(".", argument0) - 1);

// setup values
bspdata[? "success"] = true;
bspdata[? "error"] = "";
bspdata[? "has-asset"] = true;
bspdata[? "meta-filetype"] = _filetype;
bspdata[? "meta-filename"] = _filename;
bspdata[? "meta-map-name"] = _filename;
bspdata[? "meta-debug-log"] = "ZBSP_LOAD_MAP() BEGIN... FILE : [" + string(argument0) + "]#===#";

if (!file_exists(argument0))
{
    /// file doesn't exist : bail out
    zbsp_append_log(bspdata, "FILE NOT EXISTS... ABORT");
    
    bspdata[? "success"] = false;
    bspdata[? "error"] = "NOFILE";
    zbsp_append_log(bspdata, "ZBSP_LOAD_MAP() END");
    return bspdata;
}

// ==================================================================
/// unpack zip/pk3 file before processing
// ==================================================================
var _datafolder = "bspdata";
var _mapfolder = _datafolder + "\map\" + _filename;
var _assetfolder = _datafolder + "\res";
var _bspfile = "";

// add map resource direction to the map so we can access those later
bspdata[? "meta-res-dir"] = _mapfolder;

if (!directory_exists(_assetfolder))
{
    zbsp_append_log(bspdata, "Unpacking base assets... this might take a while");
    show_debug_message("Unpacking base assets... this might take a while");
    
    zip_unzip("hires_assets.pk3", _assetfolder);
}

zbsp_append_log(bspdata, "Unpacking level...");
zbsp_append_log(bspdata, "FILETYPE : " + _filetype);
show_debug_message("Unpacking level...");

// create directory if needed
if (!directory_exists(_datafolder))
{
    directory_create(_datafolder);
    zbsp_append_log(bspdata, "Data folder not found; Making one...");
}
if (!directory_exists(_mapfolder))
{
    directory_create(_mapfolder);
    zbsp_append_log(bspdata, "Map folder not found; Making one...");
}

zbsp_append_log(bspdata, "DATA FOLDER : " + _datafolder);
show_debug_message("DATA FOLDER : " + _datafolder);

// Do appropriate processings for each given file types
var _result = 0;
switch (_filetype)
{
    case "zip": // zip files - contains pk3 which contains the bsp file and textures
        if (!directory_exists(_mapfolder + "\unzip")) directory_create(_mapfolder + "\unzip");
        
        _result |= zip_unzip(argument0, _mapfolder + "\unzip"); // unzip contents (normally map preview pictures and info)
        _result |= zip_unzip(_mapfolder + "\unzip\" + _filename + ".pk3", _mapfolder); // unzip pk3 contents (normally bsp file and textures, etc)
        _result |= file_exists(_mapfolder + "\maps\" + _filename + ".bsp");
        _bspfile = _mapfolder + "\maps\" + _filename + ".bsp";
        break;

    default:
    case "pk3": // various pk files that contains bsp files and stuff
        _result |= zip_unzip(argument0, _mapfolder); // unzip pk3 contents (normally bsp file and textures, etc)
        _result |= file_exists(_mapfolder + "\maps\" + _filename + ".bsp");
        _bspfile = _mapfolder + "\maps\" + _filename + ".bsp";
        break;
        
    case "bsp":
        show_debug_message("BSP FILE");
        _result = 1;
        _bspfile = argument0;
        
        bspdata[? "has-asset"] = false;
        break;
}
if (_result == 0)
{
    /// possibly wrong file type
    zbsp_append_log(bspdata, "INVALID FILE... ABORT");
    
    bspdata[? "success"] = false;
    bspdata[? "error"] = "FILEINVALID";
    show_debug_message("ABORT");
    zbsp_append_log(bspdata, "ZBSP_LOAD_MAP() END");
    return bspdata;
}

// Read map related data
// .arena metadata
var _mapscriptdir = _mapfolder + "\scripts";
if (directory_exists(_mapscriptdir))
{
    var _arenadir = _mapscriptdir + "\" + _filename + ".arena";
    
    if (file_exists(_arenadir))
    {
        show_debug_message("Reading map .arena file..");
        
        var _arenafile = file_text_open_read(_arenadir);
        var _arenacontent = "";
        while (!file_text_eof(_arenafile))
        {
            var _ln = file_text_readln(_arenafile);
            _arenacontent += _ln;
            
            // Found map's actual name
            if (string_pos("longname", _ln) != 0)
            {
                var _quotebegin = string_pos('"', _ln);
                var _quoteend = string_pos('"', string_delete(_ln, 1, _quotebegin));
                var _content = string_copy(_ln, _quotebegin + 1, _quoteend - 1);
                
                bspdata[? "meta-arena-longname"] = _content;
            }
            
            // Found map's map file
            if (string_pos("map", _ln) != 0)
            {
                var _quotebegin = string_pos('"', _ln);
                var _quoteend = string_pos('"', string_delete(_ln, 1, _quotebegin));
                var _content = string_copy(_ln, _quotebegin + 1, _quoteend - 1);
                
                bspdata[? "meta-arena-map"] = _content;
            }
            
            // Found map's type / tag list
            if (string_pos("type", _ln) != 0)
            {
                var _quotebegin = string_pos('"', _ln);
                var _quoteend = string_pos('"', string_delete(_ln, 1, _quotebegin));
                var _content = string_copy(_ln, _quotebegin + 1, _quoteend - 1);
                
                // Split them into the lists
                var _typelist = ds_list_create();
                var _blankpos = string_pos(" ", _content);
                
                while (_blankpos != 0)
                {
                    ds_list_add(_typelist, string_copy(_content, 1, _blankpos - 1));
                    _content = string_delete(_content, 1, _blankpos);
                    _blankpos = string_pos(" ", _content);
                }
                ds_list_add(_typelist, _content);
                
                bspdata[? "meta-arena-type"] = _typelist;
            }
            
            // Found map's bot list
            if (string_pos("bots", _ln) != 0)
            {
                var _quotebegin = string_pos('"', _ln);
                var _quoteend = string_pos('"', string_delete(_ln, 1, _quotebegin));
                var _content = string_copy(_ln, _quotebegin + 1, _quoteend - 1);
                
                // Split them into the lists
                var _botslist = ds_list_create();
                var _blankpos = string_pos(" ", _content);
                
                while (_blankpos != 0)
                {
                    ds_list_add(_botslist, string_copy(_content, 1, _blankpos - 1));
                    _content = string_delete(_content, 1, _blankpos);
                    _blankpos = string_pos(" ", _content);
                }
                ds_list_add(_botslist, _content);
                
                bspdata[? "meta-arena-bots"] = _botslist;
            }
        }
        file_text_close(_arenafile);
        
        bspdata[? "meta-arena"] = _arenacontent;
        bspdata[? "meta-map-name"] = bspdata[? "meta-arena-longname"];
    }
    else
    {
        show_debug_message("Can't find / read .arena file.. :(");
    }
}

// Fetch map's texture assets direction while walking along the map folder.
// since this data is unused but I'm leaving this mass of data to use in other cases, You may comment this chunk of code, If you want to juice out the last bits of loading time.
var _maptexdir = _mapfolder + "\textures";
if (directory_exists(_maptexdir))
{
    var _dirqueue = ds_queue_create();
    var _texdirlist = ds_list_create();
    
    ds_queue_enqueue(_dirqueue, _maptexdir);
    while (!ds_queue_empty(_dirqueue)) // Search all sub-directory for files/image
    {
        var _texdir = ds_queue_dequeue(_dirqueue);
        
        zbsp_append_log(bspdata, "DIRECTORY : " + _texdir);
        
        // Enqueue all possible directories first
        var _subdir = file_find_first(_texdir + "\*", fa_directory);
        while (_subdir != "")
        {
            if (directory_exists(_texdir + "\" + _subdir))
            {
                zbsp_append_log(bspdata, "> SUBDIRECTORY : " + _subdir);
                ds_queue_enqueue(_dirqueue, _texdir + "\" + _subdir);
            }
            
            _subdir = file_find_next();
        }
        file_find_close();
        
        // And find all files after that
        var _dirimg = file_find_first(_texdir + "\*.*", 0);
        while (_dirimg != "")
        {
            if (file_exists(_texdir + "\" + _dirimg))
            {
                zbsp_append_log(bspdata, "+ FILE : " + _dirimg);
                ds_list_add(_texdirlist, _texdir + "\" + _dirimg);
            }
            
            _dirimg = file_find_next();
        }
        file_find_close();
    }
    ds_queue_destroy(_dirqueue);
    bspdata[? "meta-res-tex-dir"] = _texdirlist;
}


// ==================================================================
/// (Finally) Begin loading from BSP file
// ==================================================================
var _filebuffer = buffer_load(_bspfile);
buffer_seek(_filebuffer, buffer_seek_start, 0);

// Header
zbsp_append_log(bspdata, "[HEADER] ===================");
var _magic = zbsp_read_str(_filebuffer, 4); zbsp_append_log(bspdata, "MAGIC STRING : " + _magic); show_debug_message("MAGIC STRING : " + _magic);
var _version = buffer_read(_filebuffer, buffer_s32); zbsp_append_log(bspdata, "BSP VERSION : " + string(_version)); show_debug_message("BSP VERSION : " + string(_version));
if (_magic != "IBSP")
{
    /// possibly wrong file type
    zbsp_append_log(bspdata, "INVALID FILE... ABORT");
    
    bspdata[? "success"] = false;
    bspdata[? "error"] = "FILEINVALID";
    show_debug_message("ABORT");
    zbsp_append_log(bspdata, "ZBSP_LOAD_MAP() END");
    return bspdata;
}

// Lumps data
zbsp_load_lump_directory(_filebuffer, bspdata);

// Load entities data
zbsp_load_lump_entities(_filebuffer, bspdata);
var _file = file_text_open_write(_mapfolder + "\entities-dump.txt");
file_text_write_string(_file, bspdata[? "entities"]);
file_text_close(_file);

// Load textures data
show_debug_message("Loading textures..");
zbsp_load_lump_textures(_filebuffer, bspdata);

// Load plane data
show_debug_message("Loading planes..");
zbsp_load_lump_planes(_filebuffer, bspdata);

// Load node data
show_debug_message("Loading nodes..");
zbsp_load_lump_nodes(_filebuffer, bspdata);

// Load leaf data
show_debug_message("Loading leafs..");
zbsp_load_lump_leafs(_filebuffer, bspdata);

// Load leaffaces data
show_debug_message("Loading leaffaces..");
zbsp_load_lump_leaffaces(_filebuffer, bspdata);

// Load leafbrushes data
show_debug_message("Loading leafbrushes..");
zbsp_load_lump_leafbrushes(_filebuffer, bspdata);

// Load models data
show_debug_message("Loading models..");
zbsp_load_lump_models(_filebuffer, bspdata);

// Load brushes data
show_debug_message("Loading brushes..");
zbsp_load_lump_brushes(_filebuffer, bspdata);

// Load brushsides data
show_debug_message("Loading brushsides..");
zbsp_load_lump_brushsides(_filebuffer, bspdata);

// Load vertices data
show_debug_message("Loading vertices..");
zbsp_load_lump_vertices(_filebuffer, bspdata);

// Load meshverts data
show_debug_message("Loading meshverts..");
zbsp_load_lump_meshverts(_filebuffer, bspdata);

// Load effects data
show_debug_message("Loading effects..");
zbsp_load_lump_effects(_filebuffer, bspdata);

// Load faces data
show_debug_message("Loading faces..");
zbsp_load_lump_faces(_filebuffer, bspdata);

// Load lightmaps data
show_debug_message("Loading lightmaps..");
zbsp_load_lump_lightmaps(_filebuffer, bspdata);

// Load lightvols data
show_debug_message("Loading lightvols..");
zbsp_load_lump_lightvols(_filebuffer, bspdata);

// Load visdata
show_debug_message("Loading visdata..");
zbsp_load_lump_visdata(_filebuffer, bspdata);


// ==================================================================
/// Process textures information
// ==================================================================
// Fetch & Load textures required for map
zbsp_append_log(bspdata, "Loading textures data.. (" + string(bspdata[? "textures-num"]) + " textures)");
show_debug_message("Loading textures data.. (" + string(bspdata[? "textures-num"]) + " textures)");

// List of texture loaded into sprites & it's texture index (for use in rendering)
bspdata[? "textures-list"] = ds_list_create();
bspdata[? "textures-sprites"] = ds_list_create();

var _textures = bspdata[? "textures-data"];
for (var i=0; i<bspdata[? "textures-num"]; i++)
{
    var _asset = zbsp_fetch_asset_dir(bspdata, _textures[# eBSP_TEXTURE.DIRECTORY, i] + ".jpg");
    
    if (_asset != "")
    {
        zbsp_append_log(bspdata, "Found texture [" + _asset + "]");
        show_debug_message("Found texture [" + _asset + "]");
        
        var _spr = sprite_add(_asset, 1, false, false, 0, 0);
        ds_list_add(bspdata[? "textures-sprites"], _spr);
        ds_list_add(bspdata[? "textures-list"], sprite_get_texture(_spr, 0));
    }
    else
    {
        zbsp_append_log(bspdata, "Can't find [" + _textures[# eBSP_TEXTURE.DIRECTORY, i] + "]!");
        show_debug_message("Can't find [" + _textures[# eBSP_TEXTURE.DIRECTORY, i] + "]!");
        
        ds_list_add(bspdata[? "textures-list"], sprite_get_texture(sprNotexture, 0)); // You can replace sprNotexture to a default error texture.
    }
}

// ==================================================================
/// Build lightmap atlas
// ==================================================================
zbsp_append_log(bspdata, "BUILDING LIGHTMAP ATLAS (" + string(bspdata[? "lightmaps-num"]) + " TEXTURES)");
show_debug_message("BUILDING LIGHTMAP ATLAS (" + string(bspdata[? "lightmaps-num"]) + " TEXTURES)");

// List of lightmaps texture index (for use in rendering)
bspdata[? "lightmaps-list"] = ds_list_create();

// Lightmap informations and Lightmap sprites
var _lmspr = bspdata[? "lightmaps-sprites"];
var _lightinfos = bspdata[? "lightmaps-data"];

// Calculate desired lightmap texture dimensions from number of lightmaps
var _npowerw = 9, _npowerh = 9; // nth power of 2 for texture atlas' dimension, 2^9 = 512; 512x512 lightmap size for default.
var _lightmapoffsety = 10; // 10px lightmap offset for considering default, No-lightmap texture
var _lightmapmargin = 4; // Space between lightmaps, in pixel
var _lightmapunit = 128 + _lightmapmargin * 2; // size of each lightmaps

var _surfwid = 1 << _npowerw, _surfhei = 1 << _npowerh; // width / height of lightmap texture atlas
var _cols = _surfwid div _lightmapunit; // maximum columns of lightmaps we can fit in
var _rows = (_surfhei - _lightmapoffsety) div _lightmapunit; // maximum rows of lightmaps we can fit in

// Expand the lightmap texture until we can fit all the lightmaps
var _surfacegrowflip = 1;
while (_cols * _rows < bspdata[? "lightmaps-num"])
{
    if (_surfacegrowflip)
    {
        _npowerw++;
        _surfwid = 1 << _npowerw;
        _cols = _surfwid div _lightmapunit;
    }
    else
    {
        _npowerh++;
        _surfhei = 1 << _npowerh;
        _rows = (_surfhei - _lightmapoffsety) div _lightmapunit;
    }
    
    _surfacegrowflip *= -1;
}

// Calculate inverse surface sizes (for calculating texture uvs)
var _invsurfwid = 1 / _surfwid, _invsurfhei = 1 / _surfhei;

// Build lightmap
var _lmapsurf = surface_create(_surfwid, _surfhei);

surface_set_target(_lmapsurf);
draw_clear(c_black);

// Draw default / no lightmap texture
draw_sprite(sprNotexture, 0, 0, 0);

// Draw the lightmaps
for (var i=0; i<bspdata[? "lightmaps-num"]; i++)
{
    var _spr = _lmspr[| i];
    ds_list_add(bspdata[? "lightmaps-list"], sprite_get_texture(_spr, 0));
    
    // draw lightmap in appropirate positions
    var _wx = (i % _cols) * _lightmapunit;
    var _wy = (i div _cols) * _lightmapunit + _lightmapoffsety;
    
    for (var o = -_lightmapmargin; o <= _lightmapmargin; o++)
    {
        draw_sprite(_spr, 0, _wx + o, _wy);
        draw_sprite(_spr, 0, _wx, _wy + o);
        draw_sprite(_spr, 0, _wx + o, _wy + o);
    }
    
    draw_sprite(_spr, 0, _wx, _wy);
    
    // calculate lightmap uvs
    _lightinfos[# eBSP_LIGHTMAP.UV_MIN_X, i] = (_wx - 1) * _invsurfwid;
    _lightinfos[# eBSP_LIGHTMAP.UV_MIN_Y, i] = (_wy - 1) * _invsurfhei;
    _lightinfos[# eBSP_LIGHTMAP.UV_MAX_X, i] = (_wx + 128 - 1) * _invsurfwid;
    _lightinfos[# eBSP_LIGHTMAP.UV_MAX_Y, i] = (_wy + 128 - 1) * _invsurfhei;
}

surface_reset_target();

bspdata[? "res-lightatlas-surf"] = _lmapsurf;
bspdata[? "res-lightatlas-spr"] = sprite_create_from_surface(_lmapsurf, 0, 0, _surfwid, _surfhei, false, false, 0, 0);
bspdata[? "res-lightatlas-tex"] = sprite_get_texture(bspdata[? "res-lightatlas-spr"], 0);

// ==================================================================
/// Build debug vertex buffer / meshses
// ==================================================================
if (builddebugmesh)
{
    var _models = bspdata[? "models-data"];
    var _leafs = bspdata[? "leafs-data"];
    var _nodes = bspdata[? "nodes-data"];
    
    // Prepare vertex format
    vertex_format_begin();
    vertex_format_add_position_3d();
    vertex_format_add_color();
    var _debugVF = vertex_format_end();
    var _debugVB;
    
    
    /// Build lightvol visualization vertex buffer
    // http://www.mralligator.com/q3/#Lightvols
    zbsp_append_log(bspdata, "Building Lightvol VB..");
    show_debug_message("Building Lightvol VB..");
    _debugVB = vertex_create_buffer();
    
    // list containing lightvolume
    var _lightvols = bspdata[? "lightvols-data"];
    
    var _lightvolcellsizeh = 64; // cell size of lightvol grid in x-y axis (constant)
    var _lightvolcellsizev = 128; // cell size of lightvol grid in z axis (constant)
    var _lightvolnx = floor(_models[# eBSP_MODEL.BBOX_MAX_X, 0] / _lightvolcellsizeh) - ceil(_models[# eBSP_MODEL.BBOX_MIN_X, 0] / _lightvolcellsizeh) + 1;
    var _lightvolny = floor(_models[# eBSP_MODEL.BBOX_MAX_Y, 0] / _lightvolcellsizeh) - ceil(_models[# eBSP_MODEL.BBOX_MIN_Y, 0] / _lightvolcellsizeh) + 1;
    var _lightvolnz = floor(_models[# eBSP_MODEL.BBOX_MAX_Z, 0] / _lightvolcellsizev) - ceil(_models[# eBSP_MODEL.BBOX_MIN_Z, 0] / _lightvolcellsizev) + 1;
    
    zbsp_append_log(bspdata, "Lightvol entries number calculated : " + string(_lightvolnx * _lightvolny * _lightvolnz));
    zbsp_append_log(bspdata, "Lightvol actual entries according to DIRENTRY : " + string(bspdata[? "lightvols-num"]));
    show_debug_message("Lightvol entries number calculated : " + string(_lightvolnx * _lightvolny * _lightvolnz));
    show_debug_message("Lightvol actual entries according to DIRENTRY : " + string(bspdata[? "lightvols-num"]));
    
    vertex_begin(_debugVB, _debugVF);
    
    var _boxsz = 8; // size of each cube
    for (var _x=0; _x<_lightvolnx; _x++)
    {
        for (var _y=0; _y<_lightvolny; _y++)
        {
            for (var _z=0; _z<_lightvolnz; _z++)
            {
                // 3d array index for accessing lightvol from list
                var _lvolidx = _z * (_lightvolnx * _lightvolny) + _y * (_lightvolnx) + _x;
                
                // world-space position of lightvol cube
                var _wpx = _models[# eBSP_MODEL.BBOX_MIN_X, 0] + _x * _lightvolcellsizeh, _wpy = -_models[# eBSP_MODEL.BBOX_MIN_Y, 0] - _y * _lightvolcellsizeh, _wpz = _models[# eBSP_MODEL.BBOX_MIN_Z, 0] + _z * _lightvolcellsizev;
                
                zbsp_vb_lightmapcube(_debugVB, _wpx, _wpy, _wpz, _boxsz, _lightvols[# eBSP_LIGHTVOL.AMBIENT, _lvolidx], _lightvols[# eBSP_LIGHTVOL.DIRECTION, _lvolidx], _lightvols[# eBSP_LIGHTVOL.PHI, _lvolidx], _lightvols[# eBSP_LIGHTVOL.THETA, _lvolidx]);
            }
        }
    }
    
    vertex_end(_debugVB);
    if (vertex_get_buffer_size(_debugVB) > 0)
    {
        vertex_freeze(_debugVB);
    }
    
    // put the vertex buffer into the map
    bspdata[? "vb-debug-lightvol"] = _debugVB;
    
    
    /// Build leaf bounding box visualization vertex buffer
    zbsp_append_log(bspdata, "Building BoundingBox VB..");
    show_debug_message("Building BoundingBox VB..");
    _debugVB = vertex_create_buffer();
    
    vertex_begin(_debugVB, _debugVF);
    
    // append models bbox data
    for (var i=0; i<bspdata[? "models-num"]; i++)
    {
        var _minx = _models[# eBSP_MODEL.BBOX_MIN_X, i], _miny = _models[# eBSP_MODEL.BBOX_MIN_Y, i], _minz = _models[# eBSP_MODEL.BBOX_MIN_Z, i];
        var _maxx = _models[# eBSP_MODEL.BBOX_MAX_X, i], _maxy = _models[# eBSP_MODEL.BBOX_MAX_Y, i], _maxz = _models[# eBSP_MODEL.BBOX_MAX_Z, i];
        
        zbsp_vb_wirecube(_debugVB, _minx, _miny, _minz, _maxx, _maxy, _maxz, c_lime, true);
    }
    
    // append nodes bbox data
    for (var i=0; i<bspdata[? "nodes-num"]; i++)
    {
        var _minx = _nodes[# eBSP_NODE.BBOX_MIN_X, i], _miny = _nodes[# eBSP_NODE.BBOX_MIN_Y, i], _minz = _nodes[# eBSP_NODE.BBOX_MIN_Z, i];
        var _maxx = _nodes[# eBSP_NODE.BBOX_MAX_X, i], _maxy = _nodes[# eBSP_NODE.BBOX_MAX_Y, i], _maxz = _nodes[# eBSP_NODE.BBOX_MAX_Z, i];
        
        zbsp_vb_wirecube(_debugVB, _minx, _miny, _minz, _maxx, _maxy, _maxz, c_lime, false);
    }
    
    // append leafs bbox data
    for (var i=0; i<bspdata[? "leafs-num"]; i++)
    {
        var _minx = _leafs[# eBSP_LEAF.BBOX_MIN_X, i], _miny = _leafs[# eBSP_LEAF.BBOX_MIN_Y, i], _minz = _leafs[# eBSP_LEAF.BBOX_MIN_Z, i];
        var _maxx = _leafs[# eBSP_LEAF.BBOX_MAX_X, i], _maxy = _leafs[# eBSP_LEAF.BBOX_MAX_Y, i], _maxz = _leafs[# eBSP_LEAF.BBOX_MAX_Z, i];
        
        zbsp_vb_wirecube(_debugVB, _minx, _miny, _minz, _maxx, _maxy, _maxz, c_lime, false);
    }
    
    vertex_end(_debugVB);
    if (vertex_get_buffer_size(_debugVB) > 0)
    {
        vertex_freeze(_debugVB);
    }
    
    // put the vertex buffer into the map
    bspdata[? "vb-debug-bbox"] = _debugVB;
    bspdata[? "vb-debug-format"] = _debugVF;
    //vertex_format_delete(_debugVF);
}


// ==================================================================
/// Build vertex buffer / meshses
// ==================================================================
// lightmap or vertex colour? - 0 : lightmap, 1 : vertex colour
var _lightmodel = 0;

// lookup table for iterating & weaving a vertex patch
var _patchidxlut = -1;

// lightmap default uv
var _lmapdefaultuv = 1 * _invsurfwid;
if (buildlevelmesh)
{
    // Build vertex buffer of faces
    zbsp_append_log(bspdata, "Building Faces VB..");
    show_debug_message("Building Faces VB..");
    
    var _textureready = bspdata[? "has-asset"];
    
    // Prepare vertex format
    if (_textureready)
    {
        vertex_format_begin();
        vertex_format_add_position_3d();
        vertex_format_add_colour();
        vertex_format_add_textcoord();
        vertex_format_add_custom(vertex_type_float2, vertex_usage_textcoord);
        vertex_format_add_normal();
        var _debugVF = vertex_format_end();
    }
    else
    {
        vertex_format_begin();
        vertex_format_add_position_3d();
        vertex_format_add_colour();
        vertex_format_add_textcoord();
        vertex_format_add_normal();
        var _debugVF = vertex_format_end();
        
        show_debug_message("NO ASSETS");
    }
    
    var _faces = bspdata[? "faces-data"];
    var _vertices = bspdata[? "vertices-data"];
    var _meshverts = bspdata[? "meshverts-data"];
    var _lmapinfo = bspdata[? "lightmaps-data"];
    
    bspdata[? "faces-buffers"] = ds_list_create();
    for (var i=0; i<bspdata[? "faces-num"]; i++)
    {
        // Fetch face type
        var _type = _faces[# eBSP_FACE.TYPE, i];
        
        // Create a vertexbuffer for face
        var _debugVB = vertex_create_buffer();
        vertex_begin(_debugVB, _debugVF);
        
        // Fetch first meshverts idx and number
        var _meshvertidx = _faces[# eBSP_FACE.MESHVERT_IDX, i], _meshvertnum = _faces[# eBSP_FACE.MESHVERT_NUM, i];
        
        // Fetch first vertex idx and number
        var _vertidx = _faces[# eBSP_FACE.VERTEX_IDX, i], _vertnum = _faces[# eBSP_FACE.VERTEX_NUM, i];
        
        // Fetch texture index
        var _texidx = _faces[# eBSP_FACE.TEXTURE, i];
        if (_texidx < 0)
        {
            show_debug_message("FACE #" + string(i) + " TEX : " + string(_texidx));
        }
        
        // Fetch lightmap index
        var _lmapidx = _faces[# eBSP_FACE.LIGHTMAP, i];
        var _lmapu, _lmapv, _lmapusz, _lmapvsz;
        
        if (_lmapidx < 0)
        {
            // Not using lightmap? use default notexture uv instead..
            _lmapu = _lmapdefaultuv;
            _lmapv = _lmapdefaultuv;
            _lmapusz = _lmapdefaultuv;
            _lmapvsz = _lmapdefaultuv;
        }
        else
        {
            // UV starting coords
            _lmapu = _lmapinfo[# eBSP_LIGHTMAP.UV_MIN_X, _lmapidx];
            _lmapv = _lmapinfo[# eBSP_LIGHTMAP.UV_MIN_Y, _lmapidx];
            
            // UV size
            _lmapusz = _lmapinfo[# eBSP_LIGHTMAP.UV_MAX_X, _lmapidx] - _lmapu;
            _lmapvsz = _lmapinfo[# eBSP_LIGHTMAP.UV_MAX_Y, _lmapidx] - _lmapv;
        }
        
        // Default vertex colour when using lightmap
        // (vertex colour is mostly only used for lighting)
        var _vertexcol = c_white;
        
        /// Type dependant vertex building code
        switch (_type)
        {
            case eBSP_FACE_TYPE.MESH: // Mesh
            case eBSP_FACE_TYPE.POLYGON: // Polygon (Thankfully they've already triangulated the polygons)
                if (_textureready)
                {
                    for (var j=0; j<_meshvertnum; j++)
                    {
                        // Calculate current vertex index from meshvert offset & first vertex index
                        var _cvertex = _vertidx + _meshverts[| _meshvertidx + j];
                        
                        // Append vertex to buffer
                        vertex_position_3d(_debugVB, _vertices[# eBSP_VERTEX.X, _cvertex], _vertices[# eBSP_VERTEX.Y, _cvertex], _vertices[# eBSP_VERTEX.Z, _cvertex]);
                        
                        if (_lightmodel == 0)
                        {
                            vertex_colour(_debugVB, _vertexcol, 1);
                        }
                        else
                        {
                            vertex_colour(_debugVB, _vertices[# eBSP_VERTEX.COLOUR, _cvertex], _vertices[# eBSP_VERTEX.ALPHA, _cvertex]);
                        }
                        
                        vertex_texcoord(_debugVB, _vertices[# eBSP_VERTEX.TEX_U, _cvertex], _vertices[# eBSP_VERTEX.TEX_V, _cvertex]);
                        
                        vertex_float2(_debugVB, _lmapu + _lmapusz * _vertices[# eBSP_VERTEX.LMAP_U, _cvertex], _lmapv + _lmapvsz * _vertices[# eBSP_VERTEX.LMAP_V, _cvertex]);
                        //vertex_float2(_debugVB, lerp(_lmapu, _lmapusz, _vertices[# eBSP_VERTEX.LMAP_U, _cvertex]), lerp(_lmapv, _lmapvsz, _vertices[# eBSP_VERTEX.LMAP_V, _cvertex]));
                        
                        vertex_normal(_debugVB, _vertices[# eBSP_VERTEX.NORMAL_X, _cvertex], _vertices[# eBSP_VERTEX.NORMAL_Y, _cvertex], _vertices[# eBSP_VERTEX.NORMAL_Z, _cvertex]);
                    }
                }
                else
                {
                    for (var j=0; j<_meshvertnum; j++)
                    {
                        // Calculate current vertex index from meshvert offset & first vertex index
                        var _cvertex = _vertidx + _meshverts[| _meshvertidx + j];
                        
                        // Append vertex to buffer
                        vertex_position_3d(_debugVB, _vertices[# eBSP_VERTEX.X, _cvertex], _vertices[# eBSP_VERTEX.Y, _cvertex], _vertices[# eBSP_VERTEX.Z, _cvertex]);
                        
                        if (_lightmodel == 0)
                        {
                            vertex_colour(_debugVB, _vertexcol, 1);
                        }
                        else
                        {
                            vertex_colour(_debugVB, _vertices[# eBSP_VERTEX.COLOUR, _cvertex], _vertices[# eBSP_VERTEX.ALPHA, _cvertex]);
                        }
                        
                        vertex_texcoord(_debugVB, _lmapu + _lmapusz * _vertices[# eBSP_VERTEX.LMAP_U, _cvertex], _lmapv + _lmapvsz * _vertices[# eBSP_VERTEX.LMAP_V, _cvertex]);
                        //vertex_texcoord(_debugVB, _lmapu + _lmapusz * _vertices[# eBSP_VERTEX.LMAP_U, _cvertex], _lmapv + _lmapvsz * _vertices[# eBSP_VERTEX.LMAP_V, _cvertex]);
        
                        vertex_normal(_debugVB, _vertices[# eBSP_VERTEX.NORMAL_X, _cvertex], _vertices[# eBSP_VERTEX.NORMAL_Y, _cvertex], _vertices[# eBSP_VERTEX.NORMAL_Z, _cvertex]);
                    }
                }
                break;
                
            case eBSP_FACE_TYPE.BEZIERPATCH: // Bezier patch (Draw control points for now)
                // Fetch control points grid dimensions
                var _bezierw = _faces[# eBSP_FACE.BEZIERPATCH_W, i], _bezierh = _faces[# eBSP_FACE.BEZIERPATCH_H, i];
                // var _bezierto
                
                // (DEBUG) unset texture to default
                // ds_list_set(bspdata[? "textures-list"], i, sprite_get_texture(sprNotexture, 0));
                
                // show_debug_message("PATCH #" + string(i) + " | " + string(_bezierw) + ", " + string(_bezierh) + " | " + string(_vertnum));
                
                _patchidxlut[0, 0] = 0; _patchidxlut[0, 1] = 0;
                _patchidxlut[1, 0] = 0; _patchidxlut[1, 1] = 1;
                _patchidxlut[2, 0] = 1; _patchidxlut[2, 1] = 0;
                _patchidxlut[3, 0] = 1; _patchidxlut[3, 1] = 0;
                _patchidxlut[4, 0] = 0; _patchidxlut[4, 1] = 1;
                _patchidxlut[5, 0] = 1; _patchidxlut[5, 1] = 1;
                
                // Read control points
                if (_textureready)
                {
                    for (var j=0; j<_bezierw - 1; j++)
                    {
                        for (var k=0; k<_bezierh - 1; k++)
                        {
                            // Get control points for patch
                            for (var o=0; o<6; o++)
                            {
                                // Calculate current vertex index
                                var _x = j + _patchidxlut[o, 0];
                                var _y = k + _patchidxlut[o, 1];
                                var _cvertex = _vertidx + (_x + _y * _bezierw);
                                
                                var _widlerp = _x / (_bezierw - 1);
                                var _heilerp = _y / (_bezierh - 1);
                                
                                var _col = make_color_rgb(_widlerp * 255, _heilerp * 255, 128);
                                
                                if (_cvertex > _vertnum + _vertidx)
                                {
                                    _col = c_fuchsia;
                                }
                                
                                vertex_position_3d(_debugVB, _vertices[# eBSP_VERTEX.X, _cvertex], _vertices[# eBSP_VERTEX.Y, _cvertex], _vertices[# eBSP_VERTEX.Z, _cvertex]);
                                // vertex_colour(_debugVB, _vertices[# eBSP_VERTEX.COLOUR, _cvertex], _vertices[# eBSP_VERTEX.ALPHA, _cvertex]);
                                vertex_colour(_debugVB, _col, 1);
                                vertex_texcoord(_debugVB, _vertices[# eBSP_VERTEX.TEX_U, _cvertex], _vertices[# eBSP_VERTEX.TEX_V, _cvertex]);
                                vertex_texcoord(_debugVB, _lmapu + _lmapusz * _vertices[# eBSP_VERTEX.LMAP_U, _cvertex], _lmapv + _lmapvsz * _vertices[# eBSP_VERTEX.LMAP_V, _cvertex]);
                                vertex_normal(_debugVB, _vertices[# eBSP_VERTEX.NORMAL_X, _cvertex], _vertices[# eBSP_VERTEX.NORMAL_Y, _cvertex], _vertices[# eBSP_VERTEX.NORMAL_Z, _cvertex]);
                            }
                        }
                    }
                }
                else
                {
                    for (var j=0; j<_bezierw - 1; j++)
                    {
                        for (var k=0; k<_bezierh - 1; k++)
                        {
                            // Get control points for patch
                            for (var o=0; o<6; o++)
                            {
                                // Calculate current vertex index
                                var _x = j + _patchidxlut[o, 0];
                                var _y = k + _patchidxlut[o, 1];
                                var _cvertex = _vertidx + (_x + _y * _bezierw);
                                
                                var _widlerp = _x / (_bezierw - 1);
                                var _heilerp = _y / (_bezierh - 1);
                                
                                var _col = make_color_rgb(_widlerp * 255, _heilerp * 255, 128);
                                
                                if (_cvertex > _vertnum + _vertidx)
                                {
                                    _col = c_fuchsia;
                                }
                                
                                vertex_position_3d(_debugVB, _vertices[# eBSP_VERTEX.X, _cvertex], _vertices[# eBSP_VERTEX.Y, _cvertex], _vertices[# eBSP_VERTEX.Z, _cvertex]);
                                // vertex_colour(_debugVB, _vertices[# eBSP_VERTEX.COLOUR, _cvertex], _vertices[# eBSP_VERTEX.ALPHA, _cvertex]);
                                vertex_colour(_debugVB, _col, 1);
                                vertex_texcoord(_debugVB, _lmapu + _lmapusz * _vertices[# eBSP_VERTEX.LMAP_U, _cvertex], _lmapv + _lmapvsz * _vertices[# eBSP_VERTEX.LMAP_V, _cvertex]);
                                vertex_normal(_debugVB, _vertices[# eBSP_VERTEX.NORMAL_X, _cvertex], _vertices[# eBSP_VERTEX.NORMAL_Y, _cvertex], _vertices[# eBSP_VERTEX.NORMAL_Z, _cvertex]);
                            }
                        }
                    }
                }
                break;
        }
        
        // Tidy up stuff & into the buffer list it goes
        vertex_end(_debugVB);
        if (vertex_get_buffer_size(_debugVB) > 0)
        {
            vertex_freeze(_debugVB);
        }
        
        ds_list_add(bspdata[? "faces-buffers"], _debugVB);
    }
    
    bspdata[? "vb-format"] = _debugVF;
    
    // TIL deleting vertex format & rendering the vertex buffer with deleted format will crash the game
    //vertex_format_delete(_debugVF);
}

// Free buffer from leaking the memory
buffer_delete(_filebuffer);

zbsp_append_log(bspdata, "ZBSP_LOAD_MAP() END");
return bspdata;
