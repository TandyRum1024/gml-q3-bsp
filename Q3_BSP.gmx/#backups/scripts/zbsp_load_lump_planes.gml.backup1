// 2019-08-21 21:58:52
///zbsp_load_lump_planes(buffer, map)
/*
    Loads planes lump data from given buffer into the given map
    ==========================================================
    The data can be accessed from the given map with the key "planes-data", Which referes to a ds_grid,
    with height same as the value from the map's "planes-num" value.
    Each row contains a data for each plane, With following indices:
    data[# 0-2, row] : Plane's xyz normal
    data[# 3, row] : Plane's distance from (0, 0) origin
*/

var _off = argument1[? "planes-diroff"], _len = argument1[? "planes-dirlen"];
var _num = _len / global.BSPLumpSizes[@ eBSPLUMP.PLANES], _data;
buffer_seek(argument0, buffer_seek_start, _off);

_data = ds_grid_create(4, _num);
for (var i=0; i<_num; i++)
{
    // normal
    _data[# 0, i] = buffer_read(argument0, buffer_f32);
    _data[# 1, i] = buffer_read(argument0, buffer_f32);
    _data[# 2, i] = buffer_read(argument0, buffer_f32);
    
    // distance from origin along normal
    _data[# 3, i] = buffer_read(argument0, buffer_f32);
}

argument1[? "planes-num"] = _num;
argument1[? "planes-data"] = _data;
