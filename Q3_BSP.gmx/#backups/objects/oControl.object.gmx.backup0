// 2019-08-20 07:11:10
#event create
iui_init();
z3d_init();
zbsp_init(2048, 2048);

mouseSensitivity = 0.3;
mouseLock = false;

debugDrawBbox = false;
debugDrawLvol = false;

lightmapidx = 0;

renderLeafList = ds_list_create();
renderLeafCulled = 0;
//currentLeaf = 0;

map = zbsp_load_map("heatctf.zip");

#event destroy
d3d_end();
zbsp_free_map(map);

#event step
var _scrw = window_get_width(), _scrh = window_get_height();

if (keyboard_check_pressed(ord('1')))
    debugDrawBbox = !debugDrawBbox;
    
if (keyboard_check_pressed(ord('2')))
    debugDrawLvol = !debugDrawLvol;

if (keyboard_check_pressed(ord('L')))
{
    mouseLock = !mouseLock;
    window_mouse_set(_scrw * 0.5, _scrh * 0.5);
}

if (mouseLock)
{
    var _dx = window_mouse_get_x() - (_scrw * 0.5);
    var _dy = window_mouse_get_y() - (_scrh * 0.5);
    camRotH += _dx * mouseSensitivity;
    camRotV += _dy * mouseSensitivity;
    camTilt += _dx * mouseSensitivity * 0.2;
    window_mouse_set(_scrw * 0.5, _scrh * 0.5);
    
    z3d_update_vectors();
    
    // move camera
    var _inputfwd = keyboard_check(ord('W')) - keyboard_check(ord('S'));
    var _inputside = keyboard_check(ord('D')) - keyboard_check(ord('A'));
    if (_inputfwd != 0 || _inputside != 0)
    {
        var _wishdirx = camFwdX * _inputfwd - dsin(camRotH) * _inputside;
        var _wishdiry = camFwdY * _inputfwd + dcos(camRotH) * _inputside;
        var _wishdirz = camFwdZ * _inputfwd;
        
        z3d_accelerate(0.25, 6, _wishdirx, _wishdiry, _wishdirz);
    }
    camTilt = lerp(camTilt, 0, 0.08);
}
else
{
    z3d_update_vectors();
}

// Get current leaf
var _leafs = map[? "leafs-data"];
var _nodes = map[? "nodes-data"];
var _planes = map[? "planes-data"];
var _currentnode = 0;
while (_currentnode >= 0)
{
    var _nodeplane = _nodes[# 0, _currentnode];
    var _planenx = _planes[# 0, _nodeplane], _planeny = _planes[# 1, _nodeplane], _planenz = _planes[# 2, _nodeplane];
    var _planedist = _planes[# 3, _nodeplane];
    
    var _dist = dot_product_3d(camX, camY, camZ, _planenx, _planeny, _planenz) - _planedist;
    
    if (_dist >= 0)
    {
        _currentnode = _nodes[# 1, _currentnode];
    }
    else
    {
        _currentnode = _nodes[# 2, _currentnode];
    }
}
var _currentleaf = -_currentnode - 1;
var _currentcluster = _leafs[# 0, _currentleaf];

// Calculate PVS
var _visdata = map[? "visdata"];
var _bytespercluster = map[? "visdata-size"];
ds_list_clear(renderLeafList);
renderLeafCulled = 0;

if (map[? "visdata-num"] <= 0 || map[? "visdata-size"] <= 0)
{
    for (var i=0; i<map[? "leafs-num"]; i++)
    {
        ds_list_add(renderLeafList, i);
    }
}
else
{
    for (var i=0; i<map[? "leafs-num"]; i++)
    {
        var _clustervictm = _leafs[# 0, i];
        
        // Check PVS bits
        // each cluster has _bytespercluster amount of list entries, with each bit corresponding to other clusters.
        var _vecidx = _currentcluster * _bytespercluster + (_clustervictm >> 3);
        var _visset = _visdata[| _vecidx];
        
        if (_visset & (1 << (_clustervictm & 7)))
        {
            ds_list_add(renderLeafList, i);
        }
        else
        {
            renderLeafCulled++;
        }
    }
}


camX += camVX;
camY += camVY;
camZ += camVZ;
camVX *= 0.95;
camVY *= 0.95;
camVZ *= 0.95;
iui_update_io();

#event draw Draw wireframe leaf bbox
d3d_set_projection_ext(camX, camY, camZ, camX + camFwdX, camY + camFwdY, camZ + camFwdZ, camUpX, camUpY, camUpZ, 90, window_get_width() / window_get_height(), 1, 16000);

//vertex_submit(zbspDebugMeshVB, pr_trianglelist, image_get_texture(map[? "res-texatlas"], 0));//surface_get_texture(map[? "res-lightatlas"]));
var _leafs = map[? "leafs-data"];
var _leaffaces = map[? "leaffaces-data"];
var _vertexbuffers = map[? "faces-buffers"];
var _faces = map[? "faces-data"];
var _textures = map[? "textures-list"];

texture_set_repeat(true);
texture_set_interpolation(true);

for (var i=0; i<ds_list_size(renderLeafList); i++)
{
    var _currentleaf = renderLeafList[| i];
    var _lfidx = _leafs[# 8, _currentleaf];
    var _lfnum = _leafs[# 9, _currentleaf];
    
    for (var j=0; j<_lfnum; j++)
    {
        var _faceidx = _leaffaces[| _lfidx + j];
        var _type = _faces[# 2, _faceidx];
        
        if (_type == 3 || _type == 1)
        {
            var _texidx = _faces[# 0, _faceidx];   
            var _vb = _vertexbuffers[| _faceidx];
            vertex_submit(_vb, pr_trianglelist, _textures[| _texidx]);
        }
    }
}
/*
for (var i=0; i<map[? "faces-num"]; i++)
{
    var _type = _faces[# 2, i];
    
    if (_type == 3 || _type == 1)
    {
        var _texidx = _faces[# 0, i];   
        var _vb = _vertexbuffers[| i];
        vertex_submit(_vb, pr_trianglelist, _textures[| _texidx]);
    }
}
*/
texture_set_interpolation(false);
texture_set_repeat(false);

if (debugDrawBbox)
    vertex_submit(zbspDebugBboxVB, pr_linelist, -1);

if (debugDrawLvol)
    vertex_submit(zbspDebugLightvolVB, pr_trianglelist, -1);

#event draw_gui Draw UI
var _scrw = window_get_width(), _scrh = window_get_height();
var _tophei = 160;
var _topmarginy = 50;

draw_set_font(fntMain16);
iui_begin();

iui_rect(0, 0, _scrw, _tophei, iuHellaDark);
ui_tabidx = iui_tab(0, 0, 80, 30, pack("MAP", "INFO", "DIRENTRY", "LUMPS", "LMAP", "TEX"), ui_tabidx, 2);

switch (ui_tabidx)
{
    case 0:
        iui_label_shadow(10, _topmarginy, "FILENAME : " + map[? "filename"], iuCream, 0, 1, c_black);
        iui_label_shadow(10, _topmarginy + 20, "FILETYPE : " + map[? "filetype"], iuCream, 0, 1, c_black);
        iui_label_shadow(10, _topmarginy + 40, "MAP NAME : " + map[? "map-name"], iuCream, 0, 1, c_black);
        iui_label_shadow(10, _topmarginy + 60, "SUCCESS : " + string(map[? "success"]) + " | " + map[? "error"], iuCream, 0, 1, c_black);
        break;
        
    case 1:
        iui_label_shadow(10, _topmarginy, "LEAF : " + string(ds_list_size(renderLeafList)), iuCream, 0, 1, c_black);
        iui_label_shadow(10, _topmarginy + 20, "LEAF CULLED : " + string(renderLeafCulled), iuCream, 0, 1, c_black);
        break;
        
    case 2:
        iui_rect(0, _tophei, _scrw, _scrh - _tophei, iuHellaDark);
        
        var _off = 0;
        for (var i=0; i<array_length_1d(global.BSPLumpNames); i++)
        {
            iui_label_shadow(10, _topmarginy + _off, "OFFSET : " + string(map[? global.BSPLumpNames[@i] + "-diroff"]), iuCream, 0, 1, c_black);
            iui_label_shadow(10, _topmarginy + _off + 20, "LENGTH : " + string(map[? global.BSPLumpNames[@i] + "-dirlen"]), iuCream, 0, 1, c_black);
            _off += 40;
        }
        break;
        
    case 3:
        iui_rect(0, _tophei, _scrw, _scrh - _tophei, iuHellaDark);
        
        _topmarginy += 30;
        
        draw_set_font(fntMain8);
        ui_lumptabidx = iui_tab(0, 30, 64, 30, global.BSPLumpNames, ui_lumptabidx, 1);
        draw_set_font(fntMain16);
        
        switch (ui_lumptabidx)
        {
            case eBSPLUMP.ENTITIES:
                var _drawtext = trim_text_lines(map[? "entities"], ui_entitiesscroll, ui_entitiesscroll + 20);
                if (mouse_wheel_up()) ui_entitiesscroll = max(0, ui_entitiesscroll - 1);
                if (mouse_wheel_down()) ui_entitiesscroll++;
                
                iui_label_shadow(10, _topmarginy, _drawtext, iuCream, 0, 1, c_black);
                break;
                
            case eBSPLUMP.TEXTURES:
                var _textures = map[? "textures-data"];
                var _maptextures = map[? "res-tex-dir"];
                
                iui_label_shadow(10, _topmarginy, string(map[? "textures-num"]) + " TEXTURES NEEDED", iuCream, 0, 1, c_black);
                draw_set_font(fntMain8);
                for (var i=0; i<map[? "textures-num"]; i++)
                {
                    iui_label_shadow(10, _topmarginy + 30 + 10 * i, _textures[# 0, i], iuCream, 0, 1, c_black);
                }
                draw_set_font(fntMain16);
                
                iui_label_shadow(_scrw * 0.5 + 10, _topmarginy, string(ds_list_size(_maptextures)) + " MAP TEXTURES FOUND", iuCream, 0, 1, c_black);
                draw_set_font(fntMain8);
                for (var i=0; i<ds_list_size(_maptextures); i++)
                {
                    iui_label_shadow(_scrw * 0.5 + 10, _topmarginy + 30 + 10 * i, _maptextures[| i], iuCream, 0, 1, c_black);
                }
                draw_set_font(fntMain16);
                break;
                
            case eBSPLUMP.PLANES:
                var _planes = map[? "planes-data"];
                
                iui_label_shadow(10, _topmarginy, string(map[? "planes-num"]) + " PLANES", iuCream, 0, 1, c_black);
                
                draw_set_font(fntMain8);
                for (var i=0; i<map[? "planes-num"]; i++)
                {
                    iui_label_shadow(10, _topmarginy + 30 + 10 * i, string(_planes[# 0, i]) + ", " + string(_planes[# 1, i]) + ", " + string(_planes[# 2, i]) + " | " + string(_planes[# 3, i]), iuCream, 0, 1, c_black);
                }
                draw_set_font(fntMain16);
                break;
        }
        break;
        
    case 4:
        if (keyboard_check_pressed(vk_right))
        {
            lightmapidx = (lightmapidx + 1) % map[? "lightmaps-num"];
        }
        if (keyboard_check_pressed(vk_left))
        {
            lightmapidx = (lightmapidx + map[? "lightmaps-num"] - 1) % map[? "lightmaps-num"];
        }
    
        var _cx = _scrw * 0.5, _cy = _scrh * 0.5;
        iui_rect(0, _tophei, _scrw, _scrh - _tophei, iuHellaDark);
        draw_surface_stretched(map[? "res-lightatlas"], _cx - 300, _cy - 300, 600, 600);
        
        var _lightmaps = map[? "lightmaps-data"];
        var _lx1 = _lightmaps[# 1, lightmapidx] * 600;
        var _ly1 = _lightmaps[# 2, lightmapidx] * 600;
        var _lx2 = _lightmaps[# 3, lightmapidx] * 600;
        var _ly2 = _lightmaps[# 4, lightmapidx] * 600;
        
        iui_rect_pos(_cx - 300 + _lx1, _cy - 300 + _ly1, _cx - 300 + _lx2, _cy - 300 + _ly2, c_red, 0.5);
        
        iui_align_push(1, 2);
        iui_label_shadow(_cx, _scrh - 10, "LIGHTMAP : " + string(lightmapidx), c_yellow, 0, 1, c_black);
        iui_align_pop();
        break;
        
    case 5:
        var _cx = _scrw * 0.5, _cy = _scrh * 0.5;
        iui_rect(0, _tophei, _scrw, _scrh - _tophei, iuHellaDark);
        draw_background_stretched(image_get_texturepage(map[? "res-texatlas"], 0),  _cx - 300, _cy - 300, 600, 600);
        break;
}

draw_set_font(fntMain16);
iui_align_push(2, 2);
if (mouseLock)
{
    var _wid = string_width("MOUSE LOCKED"), _hei = string_height("MOUSE LOCKED");
    iui_rect_pos(_scrw - _wid, _scrh - _hei, _scrw, _scrh, $FF00FF, 1);
    iui_label_shadow(_scrw, _scrh, "MOUSE LOCKED", c_white, 0, 1, c_black);
}
else
{
    var _wid = string_width("MOUSE UNLOCKED"), _hei = string_height("MOUSE UNLOCKED");
    iui_rect_pos(_scrw - _wid, _scrh - _hei, _scrw, _scrh, $FF00FF, 1);
    iui_label_shadow(_scrw, _scrh, "MOUSE UNLOCKED", c_white, 0, 1, c_black);
}
iui_align_pop();

iui_end();

var _amp = 64;
draw_line_color(_scrw * 0.5, _scrh * 0.5, _scrw * 0.5 + camFwdY * _amp, _scrh * 0.5 + camFwdZ * _amp, c_red, c_red);
draw_line_color(_scrw * 0.5, _scrh * 0.5, _scrw * 0.5 + camUpY * _amp, _scrh * 0.5 + camUpZ * _amp, c_blue, c_blue);